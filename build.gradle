plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'application'
    id 'idea'
}

group = 'com.pochemuto'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '16'


repositories {
    mavenCentral()
}

def getHash = {
    def p1 = 'git rev-parse HEAD'.execute()
    p1.waitFor()
    return p1.text
}

ext {
    set('springBootAdminVersion', '2.3.1')
    set('mongodb.version', '3.12.8')
    set('git_hash', getHash())
}

application {
    mainClass = 'com.pochemuto.homealone.HomealoneApplication'
}

java {
    modularity.inferModulePath = false
}

processResources {
    expand(project.properties)
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        compileClasspath += sourceSets.test.output
        runtimeClasspath += sourceSets.test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
    shouldRunAfter test
}

tasks.withType(Test) {
    useJUnitPlatform()
}

compileJava.inputs.files(processResources)

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    testImplementation 'de.flapdoodle.embed:de.flapdoodle.embed.mongo:2.2.0'

    // cache
    implementation 'org.springframework.boot:spring-boot-starter-cache'
    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.8'

    // spring boot admin
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'de.codecentric:spring-boot-admin-starter-client'

    // metrics
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'io.micrometer:micrometer-registry-prometheus'

    implementation 'org.seleniumhq.selenium:selenium-java:3.141.59'
    implementation group: 'ru.yandex.qatools.ashot', name: 'ashot', version: '1.5.4'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    // lombok
    def lombokVersion = '1.18.20'
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.google.guava:guava:30.1-jre'
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    implementation 'org.telegram:telegrambots-spring-boot-starter:5.0.1'
}


dependencyManagement {
    imports {
        mavenBom "de.codecentric:spring-boot-admin-dependencies:${springBootAdminVersion}"
    }
}

